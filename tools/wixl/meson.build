libcommon = static_library('common',
  'util.vala',
  vala_header: 'libcommon.h',
  vala_vapi: 'common.vapi',
  vala_args: [
    '--enable-experimental',
    '--pkg', 'gio-2.0',
    '--pkg', 'posix',
    '--library', 'common',
    '--vapidir=@0@'.format(meson.project_source_root() / 'vapi'),
    '--vapidir=@0@'.format(meson.current_source_dir()),
  ],
  dependencies: glib,
)

wixl = executable('wixl',
  'builder.vala',
  'msi-default.vala',
  'msi.vala',
  'preprocessor.vala',
  'wix.vala',
  'wixl.vala',
  config_h,
  vala_args: [
    '-H', 'wixl.h',
    '--use-header',
    '--enable-experimental',
    '--pkg', 'config',
    '--pkg', 'posix',
    '--vapidir=@0@'.format(meson.project_source_root()),
    '--vapidir=@0@'.format(meson.project_build_root() / 'libmsi'),
  ],
  c_args: ['-include', meson.project_build_root() / 'config.h'],
  include_directories: inc_dirs,
  link_with: libcommon,
  dependencies: [
    libmsi,
    libmsi_vapi,
    glib,
    libgcab,
    libxml,
  ],
  install: true,
)

wixl_heat = executable('wixl-heat',
  'wixl-heat.vala',
  config_h,
  vala_args: [
    '--enable-experimental',
    '--pkg', 'config',
    '--pkg', 'posix',
    '--vapidir=@0@'.format(meson.project_source_root()),
    '--vapidir=@0@'.format(meson.project_build_root() / 'libmsi'),
  ],
  c_args: ['-include', meson.project_build_root() / 'config.h'],
  include_directories: inc_dirs,
  link_with: libcommon,
  dependencies: [
    libmsi,
    libmsi_vapi,
    glib,
    libgcab,
    libxml,
  ],
  install: true,
)
